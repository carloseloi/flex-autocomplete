<?xml version="1.0" encoding="utf-8"?>
<hc:CloseableTitleWindow
	xmlns:hc="com.hillelcoren.components.*"
	xmlns:mx="http://www.adobe.com/2006/mxml"
	implements="com.hillelcoren.components.choosers.interfaces.IBrowser"
	layout="absolute" width="500" height="350" 
	showCloseButton="false" xmlns:classes="com.hillelcoren.components.choosers.classes.*">
	
	<mx:Metadata>
		[Event("BROWSER_ITEM_SELECTED")]
	</mx:Metadata>	
	
	<mx:Script>
		<![CDATA[
			import com.hillelcoren.components.Chooser;
			import com.hillelcoren.utils.StringUtils;
			import mx.utils.ObjectUtil;
			import mx.controls.dataGridClasses.DataGridColumn;
			import mx.collections.SortField;
			import mx.collections.Sort;
			import mx.collections.ArrayCollection;
			import mx.events.ListEvent;
			import mx.events.CloseEvent;
			import mx.managers.PopUpManager;
					
			[Bindable]
			private var _dataProvider:ArrayCollection;		
			private var _dataProviderChanged:Boolean;
			
			private var _isMultiSelect:Boolean;	
			private var _isMultiSelectChanged:Boolean;
					
			[Bindable]
			private var _originalSelectedItems:ArrayCollection;
			
			private var _searchStr:String
			private var _searchStrChanged:Boolean;
			
			private var _filterFunction:Function;
			private var _labelFunction:Function;
			
			public function init():void
			{
				dataGrid.doubleClickEnabled = true;
					
				dataGrid.addEventListener( ListEvent.ITEM_DOUBLE_CLICK, handleDoubleClick );
				dataGrid.addEventListener( KeyboardEvent.KEY_DOWN, handleKeyDown );
				
				searchTextInput.setFocus();								
			}
			
			public function set filterFunction( value:Function ):void
			{
				_filterFunction = value;
			}
			
			private function handleDoubleClick( event:ListEvent ):void
			{
				dispatchEvent( new Event( Chooser.BROWSER_ITEM_SELECTED ) );
			}
			
			private function handleKeyDown( event:KeyboardEvent ):void
			{
				if (dataGrid.selectedIndex > -1 && event.keyCode == Keyboard.ENTER)
				{
					dispatchEvent( new Event( Chooser.BROWSER_ITEM_SELECTED ) );
				}
			}
			
			private function handleCancelClick():void
			{
				dispatchEvent( new CloseEvent( CloseEvent.CLOSE ) );
			}
			
			private function handleSelectClick():void
			{
				dispatchEvent( new Event( Chooser.BROWSER_ITEM_SELECTED ) );
			}
			
			public function set originalSelectedItems( value:ArrayCollection ):void
			{
				_originalSelectedItems = value;	
			}
			
			[Bindable]
			public function get originalSelectedItems():ArrayCollection
			{
				return _originalSelectedItems;
			}
			
			public function get selectedItems():Array
			{
				return dataGrid.selectedItems;	
			}
			
			public function set dataProvider( value:ArrayCollection ):void
			{
				_dataProvider = value;
				_dataProviderChanged = true;
				
				invalidateProperties();				
			}
			
			public function set labelFunction( value:Function ):void
			{
				_labelFunction = value;
			}
			
			public function set isMultiSelect( value:Boolean ):void
			{
				_isMultiSelect = value;
				_isMultiSelectChanged = true;	
				
				invalidateProperties();
			}
			
			public function set searchStr( value:String ):void
			{
				_searchStr = value;
				_searchStrChanged = true;
				
				invalidateProperties();
			}
			
			override protected function commitProperties():void
			{
				super.commitProperties();
				
				if (_isMultiSelectChanged)
				{
					_isMultiSelectChanged = false;
					dataGrid.allowMultipleSelection = _isMultiSelect;					
				}
					
				if (_dataProviderChanged)
				{
					_dataProviderChanged = false;
					
					dataGrid.dataProvider = _dataProvider;
					_dataProvider.filterFunction = filterFunctionWrapper;
					_dataProvider.refresh();
				}
				
				if (_searchStrChanged && _dataProvider)
				{
					_searchStrChanged = false;
					
					searchTextInput.text = _searchStr;
					searchTextInput.setSelection( 0, _searchStr.length );
					
					_dataProvider.refresh();
				}
			}
			
			private function filterFunctionWrapper( item:Object ):Boolean
			{
				return _filterFunction( item, searchTextInput.text );
			}
			
			private function handleSearchChange():void
			{
				_dataProvider.refresh();
			}
			
		]]>
	</mx:Script>
	
	<mx:Component id="browserItemRenderer">
		<classes:BrowserItemRenderer selectedItems="{ outerDocument.originalSelectedItems }" />
	</mx:Component>
		
	<mx:VBox width="100%" height="100%">
	
		<mx:DataGrid id="dataGrid" width="100%" height="100%" itemRenderer="{ browserItemRenderer }"/>
	
		<mx:HBox verticalAlign="middle" width="100%" 
			paddingRight="10" paddingLeft="10" paddingBottom="20" paddingTop="20">
			<mx:Label text="Search"/>
			<mx:TextInput id="searchTextInput" change="handleSearchChange()"/>
			<mx:Spacer width="100%"/>
			<mx:Button id="selectButton" label="Select" click="handleSelectClick()" enabled="{ dataGrid.selectedIndex > -1 } " width="{ Consts.BUTTON_WIDTH }"/>
			<mx:Button label="Cancel" click="handleCancelClick()" width="{ Consts.BUTTON_WIDTH }"/>
		</mx:HBox>
	</mx:VBox>
	
</hc:CloseableTitleWindow>
